SimpleNounChunk := OpenRegex {
  <chunk="B-NP"> <chunk="I-NP">*
}

NounChunk := TypedOpenRegex {
  (?:@SimpleNounChunk (?:<string="of"> | <pos="POS">))* @SimpleNounChunk
}

SimpleProperNounChunk := OpenRegex {
  (?:<chunk="B-NP" & pos=/NNPS?/> <chunk="I-NP">*) | (?:<chunk="B-NP"> <chunk="I-NP">* <chunk="I-NP" & pos=/NNPS?/> <chunk="I-NP">*)
}

ProperNounChunk := TypedOpenRegex {
  (?:@SimpleNounChunk <string="of">)* @SimpleProperNounChunk
}

SingularProperNoun := OpenRegex {
  <pos="NN">* <pos="NNP">+ <pos=/NN|NNP/>*
}

// "Barack Obama is the president of the United States."
// (Barack Obama, is the president of, the United States)
// TODO: since the relational noun is consumed, this pattern doesn't apply
VerbBasedExtraction := TypedOpenRegex {
  (<Arg1>: @NounChunk)
  (<Rel>: <lemma="be"> <pos="DT">? @NounChunk <pos="IN">)
  (<Arg2>: @NounChunk)
}

// "Chris Foo, a lawyer for ASDF, is 5 feet tall."
// (Chris Foo, (is) a lawyer for, ASDF)
AppositiveExtraction := TypedOpenRegex {
  (<Arg1>: @ProperNounChunk)

  <string=",">

  (<Rel>:
    // any article
    <string=/a|an|the/>

    // adjective or noun
    <pos=/JJ|VBD|VBN|NN|NNP/>*

    // {relnoun} {preposition}
    @RelationalNoun <pos="IN">)

  // {chunk}, the relnoun may have been incorrectly identified
  // as the beginning of the chunk
  (<Arg2>: <chunk=/.-NP/> <chunk="I-NP">*)
}

// "U.S. leader Barack Obama"
// (Barack Obama, (is) leader (of), U.S.)
AdjectiveDescriptorExtraction := TypedOpenRegex {
  // adjective
  (<adj>: <pos=/JJ|VBD|VBN/>*)

  // proper noun (no prepositions)
  (<Arg2>: <pos=/NNS?|NNPS?/>+)

  (<Rel>: @RelationalNoun)

  // Proper noun (no prepositions)
  (<Arg1>: @SingularProperNoun)
}

// "America's leader Obama gave a speech."
// (Obaja, (is) leader (of), America)
PossessiveAppositiveExtraction := TypedOpenRegex {
  // proper noun (could be plural) (no preposition)
  (<Arg2>: @ProperNounChunk)

  // {possessive}
  <pos="POS">

  // {adverb} {adjective} {relnoun}
  (<Rel>: <pos="RB">* <pos=/JJ|VBD|VBN/>* @RelationalNoun)

  // {proper noun} (no preposition)
  // the proper noun is required to distinguish this noun from the previous
  // consider: Baghdad"s deputy governor (deputy is a relnoun)
  (<Arg1>: @ProperNounChunk)
}

// "America's leader, Obama, gave a speech"
// (Obama, [be] leader [of], America)
PossessiveFullAppositiveExtraction := TypedOpenRegex {
  // Proper noun (could be plural) (no preposition)
  (<Arg2>: @ProperNounChunk)

  // Possessive
  <pos="POS">

  // {adverb} {adjective} {relnoun}
  (<Rel>: <pos="RB">* <pos=/JJ|VBD|VBN/>* @RelationalNoun)

  // comma
  <string=",">

  (<Arg1>: @ProperNounChunk)
}

// "AUC"s leader is Carlos Castano"
// (Carlos Castano, (is) leader (of), AUC)
PossessiveIsExtraction := TypedOpenRegex {
  // {nouns} (no preposition)
  (<Arg2>: <pos="DT">? <pos="RB.*">* <pos="JJ.*">* <pos=/NNS?|NNPS?/>+)

  // {possessive}
  <pos="POS">

  // {adverb} {adjective} {relnoun}
  (<Rel>: <pos="RB">* <pos=/JJ|VBD|VBN/>* @RelationalNoun)

  // be
  (<lemma=/be/>)

  // {proper np chunk}
  (<Arg1>: @ProperNounChunk)
}

// "Johnny is John's son"
// (Johnny, is son (of), John)
IsPossessiveExtraction := TypedOpenRegex {
  // {nouns} (no preposition)
  (<Arg1>: @ProperNounChunk)

  (<be>:<lemma="be">)

  (<Arg2>: @ProperNounChunk)
  <pos="POS">
  (<Rel>: <pos="RB">* <pos=/JJ|VBD|VBN/>* @RelationalNoun)
}

// "The father of Johnny is John."
// (John, is the father of, Johnny)
OfIsExtraction := TypedOpenRegex {
  (<Rel>: <chunk="B-NP"> <chunk="I-NP">* <typeStart="RelationalNoun" & typeEnd="RelationalNoun">)
  (<string="of">)
  (<Arg2>: <chunk=/.-NP/> <chunk="I-NP">*)
  (<be>: <lemma="be">)
  (<Arg1>: <chunk="B-NP"> <chunk="I-NP">*)
}

// Michele, Obama's wife, promotes home gardening.
// (Michele, (is) wife (of), Obama)
PossessiveReverseExtraction := TypedOpenRegex {
  // proper noun (no preposition)
  (<Arg1>: @ProperNounChunk)

  // comma
  <string=",">

  // Noun chunk
  (<Arg2>: @NounChunk)

  // {possessive}
  <pos="POS">

  // {adverb} {adjective} {relnoun}
  (<Rel>: <pos="RB">* <pos=/JJ|VBD|VBN/>* @RelationalNoun)

  // make sure the relnoun isn't part of a larger np-chunk
  // consider: "...spokesman Suleiman Abu Ghaith , Al-Qaeda 's military chief Saif al-Adel , and two of Osama bin Laden 's sons..."
  (?:<!chunk="I-NP">|$)
}

// Obama, the US president.
// (Obama, (is) president (of), U.S.)
ProperNounAdjectiveExtraction := TypedOpenRegex {
  (<Arg1>: @ProperNounChunk)
  <string=",">
  (<string=/a|an|the/> <pos=/JJ|VBD|VBN/>*)
  (<Arg2>: <pos=/NNP|NN/>* <pos="NNP">+)
  (<Rel>: <pos="NN">* @RelationalNoun)
}
